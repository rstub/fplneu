# -*-Shell-script-*-
# Time-stamp: <2006-07-22 22:50:04 ralf> 
# Copyright 2005, 2006 Ralf Stubner
# See the file COPYING (GNU General Public License) for license
# conditions. 
#
# common definitions and commands for FPL Neu

# finish setup with names etc
@copyright=$copyright+", Copyright 2000, 2002 Diego Puga, Copyright 2004--2006 Ralf Stubner."
@copyright=AddGPL(@copyright)
@copyright=AddException(@copyright)
@fontversion="0.9.6"

SetFontNames(@fontname,@fontfamily,@fullname,@weight,@copyright,@fontversion)
# No UniqueID 
SetUniqueID(-1)

# abbreviations for OT script-lang classes
# [TtSs]cedilla => [TtSs]commaaccent
@LatinROM="MOL,ROM"
# idotless, Idotaccent
@LatinTRK="AZE,CRT,TRK"
# most features get defined for these latin languages
@LatinAll=@LatinTRK +","+ @LatinROM +",dflt"

## Anchor Classes
SetPref("PreferSpacingAccents",0)
AddAnchorClass("top",   "default", "latn{"+@LatinAll+"}", "mark", -1, "")
#AddAnchorClass("bot",   "default", "latn{"+@LatinAll+"}", "mark", -1, "")
AddAnchorClass("bot",   "default", "latn{"+@LatinAll+"}", "mark", -1, "top")
AddAnchorClass("horn",  "default", "latn{"+@LatinAll+"}", "mark", -1, "")
AddAnchorClass("topmark", "mk-mk", "latn{"+@LatinAll+"}", "mkmk", -1, "")

# delete characters with ogonek, at, copyright, registered,
# onesuperior, twosuperior, threesuperior
Select("Aogonek")
SelectMore("aogonek")
SelectMore("Eogonek")
SelectMore("eogonek")
SelectMore("Iogonek")
SelectMore("iogonek")
SelectMore("Uogonek")
SelectMore("uogonek")
SelectMore("ogonek")
SelectMore("at")
SelectMore("copyright")
SelectMore("registered")
SelectMore("onesuperior")
SelectMore("twosuperior")
SelectMore("threesuperior") 
Clear()

if( $italicangle == 0 )
    # remove mu
    Select("mu")
    Clear()
else
    # remove f
    Select("f")
    Clear()
endif

# move Delta and rename it to uni2206 
Select("Delta")
SetUnicodeValue(0x2206)

# rename paragraph
Select("paragraph")
SetUnicodeValue(0xe015)
SetGlyphName("paragraph.alt1",0)
Reencode("unicode")

# rename florin
Select("florin")
SetUnicodeValue(0xe016)
SetGlyphName("florin.currency",0)
Reencode("unicode")

# shift [Tt]commaacent
Select("Tcommaaccent")
SetUnicodeValue(0x021a)
Select("tcommaaccent")
SetUnicodeValue(0x021b)
Reencode("unicode")

# naming of [Tt]cedilla and [Tt]commaacent
# The AGL names would be [Tt]commaacent for [Tt]cedilla and uni021[ab]
# for [Tt]commaacent. We try to avoid this mess by using uni016[23]
# and uni021[ab].
Select(0x0162)
SetGlyphName("uni0162")
Select(0x0163)
SetGlyphName("uni0163")
Reencode("unicode")

# build [Tt]cedilla
Select(0x0162,0x0163)
BuildAccented()
UnlinkReference()
RemoveOverlap()
Simplify()
RoundToInt()


# cwm
Select(0x200C)
SetWidth(0)

# IJ,ij
Select("IJ","ij")
BuildComposite()
UnlinkReference()

# centigrade
Select(0x2103)
BuildComposite()

# nbspace
Select("space")
width=GlyphInfo("Width")
Select(0x00A0)
SetWidth(width)

# softhyphen
Select("hyphen")
Copy()
Select(0x00AD)
Paste()

# new dashes
Select("endash")
Copy()
# threequartersemdash
Select(0xf6de)
SetGlyphName("threequartersemdash",0)
Paste()
Move(250,0)
PasteInto()
RemoveOverlap()
Simplify()
Reencode("unicode")
# twelveudash
Select(0xe014)
SetGlyphName("twelveudash",0)
Paste()
Move(166,0)
PasteInto()
RemoveOverlap()
Simplify()
Reencode("unicode")

# Germandbls
Select("S")
width=GlyphInfo("Width")
Copy()
Select(0xe013)
SetGlyphName("germandbls.cap",0)
Paste()
Move(width,0)
PasteInto()
Reencode("unicode")

# dblverticalbar
Select("bar")
width=GlyphInfo("Width")
Copy()
Select(0x2016)
Paste()
Move(width-400,0)
PasteInto()





