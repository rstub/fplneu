#!/usr/bin/env fontforge
# Time-stamp: <2005-08-21 19:53:25 ralf> 
# Copyright 2005 Ralf Stubner
# See the file COPYING (GNU General Public License) for license
# conditions. 

# copyright, registered, Pcircle
Open("fp9b8a.sfd");
Select("copyright");
SelectMore("registered");
SelectMore(0x2117);
SelectMore("circle");
Copy();
Close();

Open("uplbi8a.pfb")
Reencode("unicode");
# proper setup with names etc
fontname="FPLNeu-BoldItalic";
fontfamily="FPL Neu";
fullname="FPL Neu Bold Italic";
copyright=$copyright+", Copyright 2000, 2002 Diego Puga, Copyright 2004, 2005 Ralf Stubner.";
copyright=AddGPL(copyright);
copyright=AddException(copyright);
fontversion="0.9";

SetFontNames(fontname,fontfamily,fullname,"",copyright,fontversion);
# No UniqueID (needs recent ff to work properly)
SetUniqueID(-1);
# use OtherSubrs from URW (needs recent ff to work properly)
ReadOtherSubrsFile("URW-OtherSubrs.ps");

# delete characters with ogonek, at
Select("Aogonek");
SelectMore("aogonek");
SelectMore("Eogonek");
SelectMore("eogonek");
SelectMore("Iogonek");
SelectMore("iogonek");
SelectMore("Uogonek");
SelectMore("uogonek");
SelectMore("ogonek");
SelectMore("at");
Clear();

# rename paragraph
Select("paragraph");
SetUnicodeValue(0xe015);
SetCharName("paragraph.alt1",0);
Reencode("unicode");

# IJ,ij
Select("IJ","ij");
BuildComposite();
UnlinkReference();

# copyright, registered, Pcircle (see at top)
Select("copyright");
SelectMore("registered");
SelectMore(0x2117);
SelectMore("circle");
Paste();

# shift [Tt]commaacent
Select("Tcommaaccent");
SetUnicodeValue(0x021a);
Select("tcommaaccent");
SetUnicodeValue(0x021b);
Reencode("unicode");

# [Tt]cedilla
# automatism doesn't work, do it by hand
Select("T");
Copy();
Select(0x0162);
Paste();
Select("cedilla");
Copy();
Select(0x0162);
PasteWithOffset(120,0);
RemoveOverlap();
Simplify();
RoundToInt();

Select("t");
Copy();
Select(0x0163);
Paste();
Select("cedilla");
Copy();
Select(0x0163);
PasteWithOffset(-10,0);
RemoveOverlap();
Simplify();
RoundToInt();

# rename [Tt]cedilla and [Tt]commaacent
# we ignore the AGL here
Select(0x0162);
SetCharName("Tcedilla",0);
Reencode("unicode");
Select(0x0163);
SetCharName("tcedilla",0);
Reencode("unicode");
Select(0x021a);
SetCharName("Tcommaaccent",0);
Reencode("unicode");
Select(0x021b);
SetCharName("tcommaaccent",0);
Reencode("unicode");

# cwm
Select(0x200C);
SetWidth(0);

# centigrade
Select(0x2103);
BuildComposite();
UnlinkReference();

# nbspace
Select("space");
width=CharInfo("Width");
Select(0x00A0);
SetWidth(width);

# softhyphen
Select("hyphen");
Copy();
Select(0x00AD);
Paste();

# ordmasculine, ordfeminine and numero
Select("macron");
Copy();
Select(0x2116);
Paste();
Scale(90,70);
RoundToInt();
Move(-70,-230);
Copy();
Select("ordmasculine");
SelectMore("ordfeminine");
PasteInto();
Select("ordmasculine");
Copy();
Select(0x2116);
Paste();
Select("N");
Copy();
width=CharInfo("Width");
Select(0x2116);
Move(width,0);
PasteInto();

# asteriskmath
Select("asterisk");
Copy();
Select(0x2217);
Paste();
Move(0,-201);

# new dashes
Select("endash");
Copy();
# threequartersemdash
Select(0xf6de);
SetCharName("threequartersemdash",0);
Paste();
Move(250,0);
PasteInto();
RemoveOverlap();
Simplify();
Reencode("unicode");
# twelveudash
Select(0xe014);
SetCharName("twelveudash",0);
Paste();
Move(166,0);
PasteInto();
RemoveOverlap();
Simplify();
Reencode("unicode");

# Germandbls
Select("S");
width=CharInfo("Width");
Copy();
Select(0xe013);
SetCharName("germandbls.cap",0);
Paste();
Move(width-47,0);
PasteInto();
Reencode("unicode");
# dblverticalbar
Select("bar");
width=CharInfo("Width");
Copy();
Select(0x2016);
Paste();
Move(width-400,0);
PasteInto();

# get new glyphs
MergeFonts("fp9bi8a-fix.sfd");
# mho
Select("Omega");
Copy();
Select(0x2127);
Paste();
Rotate(180);

# add kerning
RemoveAllKerns();
MergeKern("fp9bi8a-fix.afm");

fp9bi8a_hint();

Reencode("unicode");
SetCharCnt(65536);
Save("fp9bi8a.sfd");
Quit(0);



