#-*-Shell-script-*-
#  Time-stamp: <2006-06-25 18:53:03 ralf> 
# add ATT/OTL features to all fonts

# most features get defined for these latin languages
defaultlatn="latn{dflt,ROM}"

# substitutions for Romanian
# [Ss]cedilla -> [Ss]commaaccent
Select("Scedilla");
AddATT("Substitution", "latn{ROM}", "locl", -1, "Scommaaccent");
AddATT("Substitution", defaultlatn, "ss01", -1, "Scommaaccent");
Select("scedilla");
AddATT("Substitution", "latn{ROM}", "locl", -1, "scommaaccent");
AddATT("Substitution", defaultlatn, "ss01", -1, "scommaaccent");
if(SelectIf("scedilla.sc") == 1)
  AddATT("Substitution", "latn{ROM}", "locl", -1, "scommaaccent.sc");
  AddATT("Substitution", defaultlatn, "ss01", -1, "scommaaccent.sc");
endif

# [Tt]cedilla -> [Tt]commaaccent
Select("uni0162");
AddATT("Substitution", "latn{ROM}", "locl", -1, "uni021A");
AddATT("Substitution", defaultlatn, "ss01", -1, "uni021A");
Select("uni0163");
AddATT("Substitution", "latn{ROM}", "locl", -1, "uni021B");
AddATT("Substitution", defaultlatn, "ss01", -1, "uni021B");
if(SelectIf("uni0163.sc") == 1)
  AddATT("Substitution", "latn{ROM}", "locl", -1, "uni021B.sc");
  AddATT("Substitution", defaultlatn, "ss01", -1, "uni021B.sc");
endif

# onum, sups, sinf, numr, dnom, and caps for figures
Select("zero", "nine");
foreach
  figure=GlyphInfo("name");
  # these three could also be done with DefaultATT() 
  AddATT("Substitution", defaultlatn, "onum", -1, figure+".oldstyle");
  AddATT("Substitution", defaultlatn, "sups", -1, figure+".superior");
  AddATT("Substitution", defaultlatn, "sinf", -1, figure+".inferior");
  AddATT("Substitution", defaultlatn, "numr", -1, figure+".numerator");
  AddATT("Substitution", defaultlatn, "dnom", -1, figure+".denominator");
  # these three could /not/ be done with DefaultATT() 
  Select(figure+".oldstyle");
  AddATT("Substitution", defaultlatn, "case", -1, figure);
  AddATT("Substitution", defaultlatn, "sups", -1, figure+".superior");
  AddATT("Substitution", defaultlatn, "sinf", -1, figure+".inferior");
  AddATT("Substitution", defaultlatn, "numr", -1, figure+".numerator");
  AddATT("Substitution", defaultlatn, "dnom", -1, figure+".denominator");
  SelectFewer(figure+".oldstyle");
endloop

# numr and dnom for slash
Select("slash");
AddATT("Substitution", defaultlatn, "numr", -1, "fraction");
AddATT("Substitution", defaultlatn, "dnom", -1, "fraction");

SelectAll();
#DefaultATT("aalt");
# lc -> sc
DefaultATT("smcp");
# uc -> sc
DefaultATT("c2sc");

# i is special
# fixme: definitions for Turkish, Azerbaijani
Select("dotlessi");
AddATT("Substitution", defaultlatn, "smcp", -1, "i.sc");
