#-*-Shell-script-*-
#  Time-stamp: <2006-07-22 22:54:11 ralf> 
# add ATT/OTL features to all fonts

# onum, sups, subs, numr, dnom, and caps for figures
Select("zero", "nine")
foreach
  figure=GlyphInfo("name")
  # some of these could also be done with DefaultATT() 
  AddATT("Substitution", "latn{"+@LatinAll+"}", "onum", -1, figure+".oldstyle")
  AddATT("Substitution", "latn{"+@LatinAll+"}", "sups", -1, figure+".superior")
  AddATT("Substitution", "latn{"+@LatinAll+"}", "subs", -1, figure+".inferior")
  AddATT("Substitution", "latn{"+@LatinAll+"}", "numr", -1, figure+".numerator")
  AddATT("Substitution", "latn{"+@LatinAll+"}", "dnom", -1, figure+".denominator")
  # these could /not/ be done with DefaultATT() 
  Select(figure+".oldstyle")
  AddATT("Substitution", "latn{"+@LatinAll+"}", "case", -1, figure)
  AddATT("Substitution", "latn{"+@LatinAll+"}", "sups", -1, figure+".superior")
  AddATT("Substitution", "latn{"+@LatinAll+"}", "subs", -1, figure+".inferior")
  AddATT("Substitution", "latn{"+@LatinAll+"}", "numr", -1, figure+".numerator")
  AddATT("Substitution", "latn{"+@LatinAll+"}", "dnom", -1, figure+".denominator")
  SelectFewer(figure+".oldstyle")
endloop

# numr and dnom for slash
Select("slash")
AddATT("Substitution", "latn{"+@LatinAll+"}", "numr", -1, "fraction")
AddATT("Substitution", "latn{"+@LatinAll+"}", "dnom", -1, "fraction")

SelectAll()
#DefaultATT("aalt")
# standard ligaturs
DefaultATT("liga")
# uc -> sc
DefaultATT("c2sc")
# lc -> sc (i is special)
SelectFewer("i")
DefaultATT("smcp")
Select("i")
# keep this list up-to-date!
AddATT("Substitution", "latn{"+@LatinROM+",dflt}", "smcp", -1, "i.sc")
AddATT("Substitution", "latn{"+@LatinTRK+"}", "smcp", -1, "idotaccent.sc")
Select("dotlessi")
AddATT("Substitution", "latn{"+@LatinAll+"}", "smcp", -1, "i.sc")
# diagonal fractions
DefaultATT("frac")

# f with hook -> florin
Select("uni0192")
AddATT("Substitution", "latn{"+@LatinAll+"}", "salt", -1, "florin.currency")

# substitutions for Romanian
# [Ss]cedilla -> [Ss]commaaccent
Select("Scedilla")
AddATT("Substitution", "latn{"+@LatinROM+"}", "locl", -1, "Scommaaccent")
AddATT("Substitution", "latn{"+@LatinAll+"}", "ss01", -1, "Scommaaccent")
Select("scedilla")
AddATT("Substitution", "latn{"+@LatinROM+"}", "locl", -1, "scommaaccent")
AddATT("Substitution", "latn{"+@LatinAll+"}", "ss01", -1, "scommaaccent")
if(SelectIf("scedilla.sc") == 1)
  AddATT("Substitution", "latn{"+@LatinROM+"}", "locl", -1, "scommaaccent.sc")
  AddATT("Substitution", "latn{"+@LatinAll+"}", "ss01", -1, "scommaaccent.sc")
endif

# [Tt]cedilla -> [Tt]commaaccent
Select("uni0162")
AddATT("Substitution", "latn{"+@LatinROM+"}", "locl", -1, "uni021A")
AddATT("Substitution", "latn{"+@LatinAll+"}", "ss01", -1, "uni021A")
Select("uni0163")
AddATT("Substitution", "latn{"+@LatinROM+"}", "locl", -1, "uni021B")
AddATT("Substitution", "latn{"+@LatinAll+"}", "ss01", -1, "uni021B")
if(SelectIf("uni0163.sc") == 1)
  AddATT("Substitution", "latn{"+@LatinROM+"}", "locl", -1, "uni021B.sc")
  AddATT("Substitution", "latn{"+@LatinAll+"}", "ss01", -1, "uni021B.sc")
endif


