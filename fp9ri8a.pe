#!/usr/bin/env fontforge
# Time-stamp: <2006-07-15 14:15:46 ralf> 
# Copyright 2005, 2006 Ralf Stubner
# See the file COPYING (GNU General Public License) for license
# conditions. 

# copyright, registered, Pcircle
Open("fp9r8a.sfd");
Select("copyright");
SelectMore("registered");
SelectMore(0x2117);
SelectMore("circle");
Copy();
Close();

Open("uplri8a.pfb")
Reencode("unicode");
# copyright, registered, Pcircle (see at top)
Select("copyright");
SelectMore("registered");
SelectMore(0x2117);
SelectMore("circle");
Paste();

# Names
@fontname="FPLNeu-Italic";
@fontfamily="FPL Neu";
@fullname="FPL Neu Italic";
@weight="Regular";
# common definitions abd commands
Common();

# move mu and rename it to uni00B5
Select("mu");
SetUnicodeValue(0x00B5);
Reencode("unicode");

# [Tt]cedilla
# automatism doesn't work, do it by hand
Select("T");
Copy();
Select(0x0162);
Paste();
Select("cedilla");
Copy();
Select(0x0162);
PasteWithOffset(120,0);
RemoveOverlap();
Simplify();
RoundToInt();

Select("t");
Copy();
Select(0x0163);
Paste();
Select("cedilla");
Copy();
Select(0x0163);
PasteWithOffset(-20,0);
RemoveOverlap();
Simplify();
RoundToInt();

# ordmasculine, ordfeminine
Select("macron");
Copy();
Select(0x2116);
Paste();
Scale(90,70);
RoundToInt();
Move(-70,-200);
Cut();
Select("ordmasculine");
SelectMore("ordfeminine");
PasteInto();

# asteriskmath
Select("asterisk");
Copy();
Select(0x2217);
Paste();
Move(0,-234);

# get new glyphs
MergeFonts("fp9ri8a-fix.sfd");
# numero
Select("ordmasculine");
Copy();
width_o=CharInfo("Width");
Select(0x2116);
width_N=CharInfo("Width");
PasteWithOffset(width_N,0);
SetWidth(width_N + width_o);
# mho
Select(0x2126);
Copy();
Select(0x2127);
Paste();
Rotate(180);

# build numerator and denominator figures
Select("zero", "nine");
ii=0;
foreach
  # preparation
  figure=GlyphInfo("name");
  Select(figure+".superior");
  width=CharInfo("Width");
  CopyReference();
  # numerator
  Select(0xe020+ii);
  PasteWithOffset(0,-149);
  SetCharName(figure+".numerator",0);
  SetWidth(width);
  # denominator
  Select(0xe030+ii);
  PasteWithOffset(0,-429);
  SetCharName(figure+".denominator",0);
  SetWidth(width);
  # finish
  SelectFewer(0xe030+ii);
  ii++;
endloop

# hinting
fp9ri8a_hint();

# ATT/OTL
fplneu_att();
#fp9ri8a_att();

# kerning
RemoveAllKerns();
MergeKern("fp9ri8a-fix.afm");

Reencode("unicode");
SetCharCnt(65536);
Save("fp9ri8a.sfd");
Quit(0);



