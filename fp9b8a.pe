#!/usr/bin/env fontforge
# Time-stamp: <2006-07-15 15:56:59 ralf> 
# Copyright 2005, 2006 Ralf Stubner
# See the file COPYING (GNU General Public License) for license
# conditions. 

Open("uplb8a.pfb")
Reencode("unicode")
# Names
@fontname="FPLNeu-Bold"
@fontfamily="FPL Neu"
@fullname="FPL Neu Bold"
@weight="Bold"
# common definitions abd commands
Common()

# remove mu
Select("mu")
Clear()

# ordmasculine, ordfeminine
Select("macron")
Copy()
Select(0x2116)
Paste()
Scale(90,70)
RoundToInt()
Move(0,-250)
Cut()
Select("ordmasculine")
SelectMore("ordfeminine")
SetWidth(334)
CenterInWidth()
AutoHint()
PasteInto()

# asteriskmath
Select("asterisk")
Copy()
Select(0x2217)
Paste()
Move(0,-204)

# fix cacute
Select("c")
Copy()
Select("cacute")
CopyFgToBg()
Paste()
Select("acute")
Copy()
Select("cacute")
PasteWithOffset(80,0)

# get new glyphs
MergeFonts("bold-crp.sfd")
MergeFonts("fp9b8a-fix.sfd")
# numero
Select("ordmasculine")
Copy()
width_o=CharInfo("Width")
Select(0x2116)
width_N=CharInfo("Width")
PasteWithOffset(width_N,0)
SetWidth(width_N + width_o)
# mho
Select(0x2126)
Copy()
Select(0x2127)
Paste()
Rotate(180)

# build numerator and denominator figures
Select("zero", "nine")
ii=0
foreach
  # preparation
  figure=GlyphInfo("name")
  Select(figure+".superior")
  width=CharInfo("Width")
  CopyReference()
  # numerator
  Select(0xe020+ii)
  PasteWithOffset(0,-150)
  SetCharName(figure+".numerator",0)
  SetWidth(width)
  # denominator
  Select(0xe030+ii)
  PasteWithOffset(0,-413)
  SetCharName(figure+".denominator",0)
  SetWidth(width)
  # finish
  SelectFewer(0xe030+ii)
  ii++
endloop

# hinting
fp9b8a_hint()

# ATT/OTL
fplneu_att()
#fp9b8a_att()

# kerning
RemoveAllKerns()
MergeKern("fp9b8a-fix.afm")

Reencode("unicode")
SetCharCnt(65536)
Save("fp9b8a.sfd")
Quit(0)


